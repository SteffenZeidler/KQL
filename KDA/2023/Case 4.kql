// Anomaly in ClientIP count
NetworkMetrics
| summarize count() by ClientIP
| top 3 by count_ asc 
| evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)

// Visualize anomaly with timechart
NetworkMetrics
| where ipv4_is_match(ClientIP, '96.236.64.0/24') // KUANDA.ORG
| make-series count(), sum(BytesSent), sum(BytesReceived) on Timestamp step 1h
| render timechart with (ysplit=panels)

// Anomaly in dcount ClientIP
NetworkMetrics
| distinct ClientIP
| summarize dcount(ClientIP) by ClientIP=format_ipv4(ClientIP, 24)
| top 3 by dcount_ClientIP
| evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)

// Anomaly in dcount TargetIP
NetworkMetrics
| summarize dcount(TargetIP) by ClientIP
| top 3 by dcount_TargetIP
| evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)

// Anomaly in bytes sent
NetworkMetrics
| summarize Count=sum(BytesSent) by Time=bin(Timestamp, 1h), ClientIP
| summarize Count=sum(Count) by Time, ClientIP=format_ipv4(ClientIP, 24)
| summarize Score=max(Count)/avg(Count) by ClientIP
| top 3 by Score
| evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)

// with series_decompose_anomalies
NetworkMetrics
| make-series sum(BytesSent) on Timestamp step 1d by ClientIP=format_ipv4(ClientIP, 24)
| extend (flag, score, baseline) = series_decompose_anomalies(sum_BytesSent)
| extend max_score = toreal(series_stats_dynamic(score).max)
| top 5 by max_score
| evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)
| render anomalychart with(anomalycolumns=flag, series=Info)

NetworkMetrics
| lookup kind=inner
  (NetworkMetrics | distinct ClientIP | evaluate ipv4_lookup(IpInfo, ClientIP, IpCidr)) on ClientIP
| make-series sum(BytesSent) on Timestamp step 1d by Info
| extend (flag, score, baseline) = series_decompose_anomalies(sum_BytesSent)
| extend max_score = toreal(series_stats_dynamic(score).max)
| top 5 by max_score
